plugins {
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "net.ltgt.apt-idea" version "0.21"
    id "org.sonarqube" version "2.7.1"
    id "org.springframework.boot" version "2.1.5.RELEASE" apply false
    id "au.com.dius.pact" version "3.6.7" apply false
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'au.com.dius.pact'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
//        maven { url "https://repo.spring.io/milestone" }
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.5.RELEASE") {
//                bomProperty 'junit-jupiter.version', '5.4.2'
            }
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR1"
        }
        dependencies {
            dependency 'com.google.guava:guava:27.1-jre'

            dependencySet(group: 'org.mapstruct', version: '1.3.0.Final') {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }

            dependencySet(group: 'au.com.dius', version: '3.6.7') {
                entry 'pact-jvm-consumer-java8_2.12'
                entry 'pact-jvm-consumer-junit5_2.12'
                entry 'pact-jvm-provider-junit5_2.12'
            }

            dependencySet(group: 'io.springfox', version: '2.9.2') {
                entry 'springfox-swagger2'
                entry 'springfox-swagger-ui'
            }

            dependency group: 'io.springfox.ui', name: 'springfox-swagger-ui-rfc6570', version: '1.0.0'
        }
    }

//    ext['wiremock.version'] = '2.23.2'
    ext['junit-jupiter.version'] = '5.4.2'
//    ext['mockito.version'] = '2.27.0'
//    ext['assertj.version'] = '3.12.2'
//    ext['rest-assured.version'] = '4.0.0'
//    ext['tomcat.version'] = '9.0.20'
//    ext['hamcrest.version'] = '2.1'

    test {
        useJUnitPlatform()

//        systemProperty 'pact.rootDir', "$buildDir/pacts"
        systemProperty('pact.verifier.publishResults', 'true')
    }

    pact {
        publish {
            pactDirectory = "$buildDir/../target/pacts"
            pactBrokerUrl = 'http://localhost:80'
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "octopus-prime_demo"
    }
}

task devUp(type: Exec, group: 'development') {
    commandLine 'docker-compose', '-f', 'docker-compose.yml', 'up', '-d'
}

task devDown(type: Exec, group: 'development') {
    commandLine 'docker-compose', '-f', 'docker-compose.yml', 'down'
}
