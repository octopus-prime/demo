plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "net.ltgt.apt-idea" version "0.21"
    id "org.sonarqube" version "2.7.1"
    id "org.springframework.boot" version "2.1.6.RELEASE" apply false
    id "au.com.dius.pact" version "3.6.10" apply false
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'au.com.dius.pact'

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.6.RELEASE")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR2")
        }
        dependencies {
            dependency 'com.google.guava:guava:27.1-jre'

            dependencySet(group: 'org.mapstruct', version: '1.3.0.Final') {
                entry 'mapstruct'
                entry 'mapstruct-processor'
            }

            dependencySet(group: 'au.com.dius', version: '3.6.10') {
                entry 'pact-jvm-consumer-java8_2.12'
                entry 'pact-jvm-consumer-junit5_2.12'
                entry 'pact-jvm-provider-junit5_2.12'
            }

            dependencySet(group: 'io.springfox', version: '2.9.2') {
                entry 'springfox-swagger2'
                entry 'springfox-swagger-ui'
            }

            dependency group: 'io.springfox.ui', name: 'springfox-swagger-ui-rfc6570', version: '1.0.0'
        }
    }

    ext['junit-jupiter.version'] = '5.5.0'
    ext['wiremock.version'] = '2.23.2'
    ext['mockito.version'] = '2.28.2'
    ext['assertj.version'] = '3.12.2'
    ext['rest-assured.version'] = '4.0.0'
    ext['hamcrest.version'] = '2.1'
    ext['postgresql.version'] = '42.2.6'
//    ext['tomcat.version'] = '9.0.20'
//    ext['embedded-mongo.version'] = '2.2.0'

    test {
        useJUnitPlatform()

//        systemProperty 'pact.rootDir', "$buildDir/pacts"
        systemProperty('pact.verifier.publishResults', 'true')
    }

    pact {
        publish {
            pactDirectory = "$buildDir/../target/pacts"
            pactBrokerUrl = 'http://localhost:80'
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "octopus-prime_demo"
    }
}

task envUp(type: Exec, group: 'docker') {
    commandLine 'docker-compose', '-f', 'docker-compose-env.yml', 'up', '-d'
}

task envDown(type: Exec, group: 'docker') {
    commandLine 'docker-compose', '-f', 'docker-compose-env.yml', 'down'
}

task demoUp(type: Exec, group: 'docker') {
    commandLine 'docker-compose', '-f', 'docker-compose-demo.yml', 'up', '-d', '--force-recreate', '--build'
}

task demoDown(type: Exec, group: 'docker') {
    commandLine 'docker-compose', '-f', 'docker-compose-demo.yml', 'down'
}
